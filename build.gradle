
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()      
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'com.github.emcastro'
archivesBaseName = "webkitfxproxy"
version = '1.0.0'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'com.mscharhag.oleaster:oleaster-runner:0.1.2'
    testCompile 'com.mscharhag.oleaster:oleaster-matcher:0.1.2'
}

javadoc {
    // ignore missing Javadoc comments or tags
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

signArchives.onlyIf {
    project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')
}

def doUploadArchives = project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')

if(doUploadArchives) {
    uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
              authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'webkitfxproxy'
                packaging 'jar'
                description 'Typed proxying for JavaFX Webkit objects.'
                url 'https://github.com/emcastro/WebkitFXProxy'
             
                scm {
                    url 'https://github.com/emcastro/WebkitFXProxy'
                    connection 'https://github.com/emcastro/WebkitFXProxy.git'
                    developerConnection 'git@github.com:emcastro/WebkitFXProxy.git'
                }
             
                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
             
                developers {
                    developer {
                        name 'Emmanuel Castro'
                        email 'emmanuel.castro@laposte.net'
                    }
                }
            }
        }
    }
}

uploadArchives.onlyIf { doUploadArchives }

