version = '0.0.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'osgi'

repositories {
    mavenCentral()      
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'com.github.emcastro'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'com.mscharhag.oleaster:oleaster-runner:0.1.2'
    testCompile 'com.mscharhag.oleaster:oleaster-matcher:0.1.2'
}

javadoc {
    // ignore missing Javadoc comments or tags
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

signArchives.onlyIf {
    project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile')
}

def doUploadArchives = project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')

if(doUploadArchives) {
    uploadArchives {
        repositories.mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'webkitfxproxy'
                packaging 'jar'
                description 'Typed proxying for JavaFX Webkit objects.'
                url 'http://www.github.com/com.github.emcastro/WebkitFXProxy'
             
                scm {
                    url 'scm:git@github.com:com.github.emcastro/WebkitFXProxy.git'
                    connection 'scm:git@github.com:com.github.emcastro/WebkitFXProxy.git'
                    developerConnection 'scm:git@github.com:com.github.emcastro/WebkitFXProxy.git'
                }
             
                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
             
                developers {
                    developer {
                        name 'Emmanuel Castro'
                    }
                }
            }
        }
    }
}

uploadArchives.onlyIf { doUploadArchives }

